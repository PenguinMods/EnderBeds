import net.fabricmc.loom.task.FabricModJsonV1Task

plugins {
    id 'net.fabricmc.fabric-loom-remap' version "${loom_version}"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.modid
}

repositories {
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

loom {
    splitEnvironmentSourceSets()

    accessWidenerPath = file("src/main/resources/${project.modid}.classtweaker")
}

fabricApi {
    configureDataGeneration {
        createSourceSet = true
        modId = "${project.modid}"
        client = true
    }
}

loom {
    mods {
        "${project.modid}" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
            sourceSet sourceSets.datagen
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.parchment_version}")
    }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
}

processResources {
    dependsOn "generateModJson"
}

tasks.register("generateModJson", FabricModJsonV1Task) {
    outputFile = file("src/main/resources/fabric.mod.json")

    json {
        modId = project.modid
        version = project.version
        name = project.modname
        description = project.description
        author('BlockyPenguin')
        contributor("u/Zeus_Da_God", {
            contactInformation = [
                    homepage: "https://redd.it/1m1s3p9/",
                    reddit: "https://reddit.com/u/Zeus_Da_God"
            ]
        })
        contactInformation = [
                homepage: "https://mods.blockypenguin.com/ender_beds",
                issues: "https://mods.blockypenguin.com/ender_beds/issues",
                sources: "https://mods.blockypenguin.com/ender_beds/source",
                email: "mods+ender_beds@blockypenguin.com"
        ]
        licenses = ["MIT"]
        icon "assets/${project.modid}/icon.png"
        environment = "*"
        entrypoint('main', "${project.main_class}")
        entrypoint('client', "${project.client_main_class}")
        entrypoint('fabric-datagen', "${project.datagen_main_class}")
        mixin("${project.modid}.mixins.json")
        mixin({
            environment = "client"
            value = "${project.modid}.client.mixins.json"
        })
        accessWidener = "${project.modid}.classtweaker"
        depends('fabricloader', ">=${project.loader_version}")
        depends('minecraft', "~${project.minecraft_version}-")
        depends('java', '>=21')
        depends('fabric-api', ">=${project.fabric_api_version}")
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 21
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)

    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

sourcesJar {
    mustRunAfter generateModJson
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}